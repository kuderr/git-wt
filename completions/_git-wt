#compdef git-wt
#description fast git worktree manager

_git-wt() {
  local -a subcommands
  subcommands=(
    'new:Create a new worktree'
    'list:List worktrees for current repo'
    'list-all:List worktrees across all repos'
    'rm:Remove a worktree and its branch'
    'path:Print worktree path'
    'origin:Print main repo path'
    'open:Open worktree in editor'
    'clean:Remove all worktrees for current repo'
    'help:Show help'
    'version:Show version'
  )

  _arguments -C \
    '(-q --quiet)'{-q,--quiet}'[Suppress non-essential output]' \
    '1:command:->command' \
    '*::arg:->args'

  case "$state" in
    command)
      _describe 'command' subcommands
      ;;
    args)
      case "${line[1]}" in
        new)
          _arguments \
            '(-b --base)'{-b,--base}'[Base branch to fork from]:branch:__git_branch_names' \
            '(-p --prefix)'{-p,--prefix}'[Branch prefix]:prefix:' \
            '--no-branch[Use detached HEAD]' \
            '--copy-env[Copy .env files into worktree]' \
            ':name:'
          ;;
        rm|remove|path|cd|open)
          _git_wt_names
          ;;
      esac
      ;;
  esac
}

_git_wt_names() {
  local -a names
  names=("${(@f)$(git wt _names 2>/dev/null)}")
  (( ${#names} )) && _describe 'worktree' names
}

__git_branch_names() {
  local -a branches
  branches=("${(@f)$(git branch --format='%(refname:short)' 2>/dev/null)}")
  _describe 'branch' branches
}

_git-wt "$@"
